/*
 * Copyright 2018, TeamDev. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

apply plugin: 'com.google.protobuf'

dependencies {
    testProtobuf project(":web-tests")
}

idea.module {
    sourceDirs += file("${projectDir}/proto")
    sourceDirs += file("$projectDir/src")
    testSourceDirs += file("$projectDir/test")
    excludeDirs += file("$projectDir/node_modules")
    excludeDirs += file("$projectDir/.nyc_output")

    iml {
        beforeMerged { module ->
            module.dependencies.clear()
        }
        whenMerged { module ->
            module.dependencies*.exported = true
        }
    }
}

clean {
    delete "$projectDir/proto"
}

// Suppress building the JS project as a Java module.
project.compileJava.enabled = false
project.compileTestJava.enabled = false

apply from: "$rootDir/scripts/js.gradle"

buildJs.doLast {
    npm 'run', 'build'
    npm 'run', 'build-dev'
}

task linkClientProto {
    doLast {
        npm 'link', 'spine-web-client-proto'
    }

    dependsOn ':client-js-proto:link'
}

/**
 * Creates a node_modules symlink in the root of projectDir 
 * resolving all of the dependencies in code. 
 */
task createNodeModulesLink {
    doLast {
        ant.symlink(
                resource: "${projectDir}/build/node-module/node_modules",
                link: "${projectDir}/node_modules"
        )
    }
}

tasks.getByPath(':ideaModule').dependsOn createNodeModulesLink

installDependencies.dependsOn linkClientProto
